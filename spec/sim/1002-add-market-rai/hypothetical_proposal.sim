#!/usr/bin/env -S yarn repl -s

PrintTransactionLogs

-- Token holder addresses for mocking
Alias CompHolder "0x7587caefc8096f5f40acb83a09df031a018c66ec" -- Compound Team 2
Alias CompHolder2 "0x5608169973d639649196A84EE4085a708bcBf397" -- Compound Team 3
Alias TokenHolder "0xeB2629a2734e272Bcc07BDA959863f316F4bD4Cf" -- Coinbase 6
Alias CUSDCHolder "0xBE0eB53F46cd790Cd13851d5EFf43D12404d33E8" -- Binance 7

-- New oracle address
Alias NewOracle "0x65c816077C29b557BEE980ae3cC2dCE80204A0C5"

Web3Fork "https://mainnet-eth.compound.finance/@14278600" (CompHolder CompHolder2 TokenHolder CUSDCHolder)
UseConfigs mainnet

--------------------------------------------------------------------
-- Governance proposal
--------------------------------------------------------------------
-- Delegate and propose
From CompHolder (Comp Delegate CompHolder)
From CompHolder2 (Comp Delegate CompHolder)
From CompHolder (GovernorBravo GovernorBravo Propose "Add RAI Market" [(Address Comptroller) (Address Comptroller) (Address cRAI) (Address Comptroller)] [0 0 0 0] ["_setPriceOracle(address)" "_supportMarket(address)" "_setReserveFactor(uint256)" "_setMarketBorrowCaps(address[],uint256[])"] [[(Address NewOracle)] [(Address cRAI)] [250000000000000000] [[(Address cRAI)] [4000000000000000000000000]]])

-- Fast forward, vote, queue, execute
MineBlock
AdvanceBlocks 14000
From CompHolder (GovernorBravo GovernorBravo Proposal LastProposal Vote For)
AdvanceBlocks 20000
GovernorBravo GovernorBravo Proposal LastProposal Queue
IncreaseTime 604910
GovernorBravo GovernorBravo Proposal LastProposal Execute

--------------------------------------------------------------------
-- Market supported test
--------------------------------------------------------------------
Assert True (Comptroller CheckListed cRAI)

--------------------------------------------------------------------
-- Accrue interest test
--------------------------------------------------------------------
CToken cRAI AccrueInterest

--------------------------------------------------------------------
-- Mint test
--------------------------------------------------------------------
From TokenHolder (Erc20 RAI Approve (Address cRAI) 10e18)
Expect Changes (Erc20 RAI TokenBalance TokenHolder) -10e18
Expect Changes (Erc20 RAI TokenBalance cRAI) +10e18
From TokenHolder (CToken cRAI Mint 10e18)

--------------------------------------------------------------------
-- Borrow test
--------------------------------------------------------------------
-- Deposit collateral
From CUSDCHolder (Erc20 USDC Approve (Address cUSDC) 10000e6) 
From CUSDCHolder (CToken cUSDC Mint 1000e6)
From CUSDCHolder (Comptroller EnterMarkets (cUSDC))
-- Borrow the new token
Expect Changes (Erc20 RAI TokenBalance CUSDCHolder) +1e18
Expect Changes (Erc20 RAI TokenBalance cRAI) -1e18
From CUSDCHolder (CToken cRAI Borrow 1e18)

--------------------------------------------------------------------
-- Repay borrow test
--------------------------------------------------------------------
From CUSDCHolder (Erc20 RAI Approve (Address cRAI) 1e18)
Expect Changes (Erc20 RAI TokenBalance CUSDCHolder) -1e18
Expect Changes (Erc20 RAI TokenBalance cRAI) +1e18
From CUSDCHolder (CToken cRAI RepayBorrow 1e18)

--------------------------------------------------------------------
-- Redeem test  (note: 50 cToken == 1 Token initially)
--------------------------------------------------------------------
From TokenHolder (CToken cRAI Redeem 50e8)

Print "Integration ok"