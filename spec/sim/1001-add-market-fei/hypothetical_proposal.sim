#!/usr/bin/env -S yarn repl -s

PrintTransactionLogs

-- Token holder addresses for mocking
Alias CompHolder "0x7587caefc8096f5f40acb83a09df031a018c66ec" -- Compound Team 2
Alias CompHolder2 "0x5608169973d639649196A84EE4085a708bcBf397" -- Compound Team 3
Alias TokenHolder "0x0D0707963952f2fBA59dD06f2b425ace40b492Fe" -- Gate.io
Alias CUSDCHolder "0xBE0eB53F46cd790Cd13851d5EFf43D12404d33E8" -- Binance 7

-- New oracle address
Alias NewOracle "0x046728da7cb8272284238bD3e47909823d63A58D"

Web3Fork "https://mainnet-eth.compound.finance/@14097000" (CompHolder CompHolder2 TokenHolder CUSDCHolder)
UseConfigs mainnet

--------------------------------------------------------------------
-- Governance proposal
--------------------------------------------------------------------
-- Delegate and propose
From CompHolder (Comp Delegate CompHolder)
From CompHolder2 (Comp Delegate CompHolder)
From CompHolder (GovernorBravo GovernorBravo Propose "Add FEI Market" [(Address Comptroller) (Address cFEI)] [0 0] ["_supportMarket(address)" "_setReserveFactor(uint256)"] [[(Address cFEI)] [250000000000000000]])

-- Fast forward, vote, queue, execute
MineBlock
AdvanceBlocks 14000
From CompHolder (GovernorBravo GovernorBravo Proposal LastProposal Vote For)
AdvanceBlocks 20000
GovernorBravo GovernorBravo Proposal LastProposal Queue
IncreaseTime 604910
GovernorBravo GovernorBravo Proposal LastProposal Execute

--------------------------------------------------------------------
-- Market supported test
--------------------------------------------------------------------
Assert True (Comptroller CheckListed cFEI)

--------------------------------------------------------------------
-- Accrue interest test
--------------------------------------------------------------------
CToken cFEI AccrueInterest

--------------------------------------------------------------------
-- Mint test
--------------------------------------------------------------------
From TokenHolder (Erc20 FEI Approve (Address cFEI) 10e18)
Expect Changes (Erc20 FEI TokenBalance TokenHolder) -10e18
Expect Changes (Erc20 FEI TokenBalance cFEI) +10e18
From TokenHolder (CToken cFEI Mint 10e18)

--------------------------------------------------------------------
-- Borrow test
--------------------------------------------------------------------
-- Deposit collateral
From CUSDCHolder (Erc20 USDC Approve (Address cUSDC) 10000e6) 
From CUSDCHolder (CToken cUSDC Mint 1000e6)
From CUSDCHolder (Comptroller EnterMarkets (cUSDC))
-- Borrow the new token
Expect Changes (Erc20 FEI TokenBalance CUSDCHolder) +1e18
Expect Changes (Erc20 FEI TokenBalance cFEI) -1e18
From CUSDCHolder (CToken cFEI Borrow 1e18)

--------------------------------------------------------------------
-- Repay borrow test
--------------------------------------------------------------------
From CUSDCHolder (Erc20 FEI Approve (Address cFEI) 1e18)
Expect Changes (Erc20 FEI TokenBalance CUSDCHolder) -1e18
Expect Changes (Erc20 FEI TokenBalance cFEI) +1e18
From CUSDCHolder (CToken cFEI RepayBorrow 1e18)

--------------------------------------------------------------------
-- Redeem test  (note: 50 cToken == 1 Token initially)
--------------------------------------------------------------------
From TokenHolder (CToken cFEI Redeem 50e8)

Print "Integration ok"